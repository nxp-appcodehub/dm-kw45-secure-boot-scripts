@rem Copyright 2021-2025 NXP
@rem All rights reserved.

@rem SPDX-License-Identifier: BSD-3-Clause

:: Script to load OEM keys to device fuses.
ECHO off
ECHO WARNING!!! Script performs destructive operation (programs fuses), are you sure that script is updated with valid data? Press ctrl+c to cancel execution or press space to continue.
ECHO on
pause

@echo off
:select_comport
reg query "HKEY_LOCAL_MACHINE\HARDWARE\DEVICEMAP\SERIALCOMM" /v  \Device\*

set /P comport=Please select comport name: 

if "%comport%"=="" (
	goto quit
) else if /i not "%comport:~0,3%"=="com" (
	set comport=
	goto select_comport
)
@echo on

:: Update connection settings according your local configuration "-p COMX", for details see blhost User's Guide in .\doc folder.
blhost -p %comport%,460800 -- fuse-read 0x1F 32
ECHO off
ECHO The factory default value of the fuse(0x1F) is all zeros

ECHO WARNING!!!  
ECHO If the fuse has been programmed (the value isn't factory default value), the operation must be aborted, press ctrl+c to cancel execution
ECHO Otherwise, update this script's content according to RoTKTH and SB3KDK generated by script 1.generate keys.cmd
ECHO WARNING!!!
ECHO on
pause
:: Increase voltage for fuse burning
blhost -p %comport%,460800 set-property 0x16 1
:: program SB3KDK (CUST_PROD_OEMFW_ENC_SK)
blhost -p %comport%,460800 -- fuse-program 0x20 "{{7aa7ef9813b3561257b8837dab26225301df3511217f2733c71dadcd447722d1}}"
:: program RoTKTH (CUST_PROD_OEMFW_AUTH_PUK)
blhost -p %comport%,460800 -- fuse-program 0x1F "{{650d8097079ff27a3e8a2da14781b922fd8295b6c00bfa067f00e87f1a16b8b304bf710d45cbd591e2e24be83183922c}}"
::Program TZM_EN fuse (required by first phase KW45 OEM samples)
blhost -p %comport%,460800 -- fuse-program 0xD "{{1}}"
:: Set voltage to normal value
blhost -p %comport%,460800 set-property 0x16 0

pause
:quit